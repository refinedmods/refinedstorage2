buildscript {
    repositories {
        maven {
            url = 'https://maven.minecraftforge.net'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'

archivesBaseName = 'refinedstorage2-platform-forge'

minecraft {
    mappings channel: 'official', version: project.minecraftVersion

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":refinedstorage2-platform-common").sourceSets.main
                    source project(":refinedstorage2-platform-api").sourceSets.main
                    source project(":refinedstorage2-platform-abstractions").sourceSets.main
                    source project(':refinedstorage2-core-api').sourceSets.main
                    source project(':refinedstorage2-resource-api').sourceSets.main
                    source project(':refinedstorage2-storage-api').sourceSets.main
                    source project(':refinedstorage2-network-api').sourceSets.main
                    source project(':refinedstorage2-query-parser').sourceSets.main
                    source project(':refinedstorage2-grid-api').sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":refinedstorage2-platform-common").sourceSets.main
                    source project(":refinedstorage2-platform-api").sourceSets.main
                    source project(":refinedstorage2-platform-abstractions").sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraftVersion}-${project.forgeVersion}"
    compileOnly project(":refinedstorage2-platform-common")
    compileOnly project(':refinedstorage2-platform-abstractions')
    compileOnly project(':refinedstorage2-platform-api')
    implementation project(':refinedstorage2-core-api')
    implementation project(':refinedstorage2-resource-api')
    implementation project(':refinedstorage2-storage-api')
    implementation project(':refinedstorage2-network-api')
    implementation project(':refinedstorage2-query-parser')
    implementation project(':refinedstorage2-grid-api')
}

processResources {
    from project(":refinedstorage2-platform-common").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
    source(project(":refinedstorage2-platform-common").sourceSets.main.allSource)
    source(project(":refinedstorage2-platform-abstractions").sourceSets.main.allSource)
    source(project(":refinedstorage2-platform-api").sourceSets.main.allSource)
    source(project(':refinedstorage2-core-api').sourceSets.main.allSource)
    source(project(':refinedstorage2-resource-api').sourceSets.main.allSource)
    source(project(':refinedstorage2-storage-api').sourceSets.main.allSource)
    source(project(':refinedstorage2-network-api').sourceSets.main.allSource)
    source(project(':refinedstorage2-query-parser').sourceSets.main.allSource)
    source(project(':refinedstorage2-grid-api').sourceSets.main.allSource)
}

jar.finalizedBy('reobfJar')

